FUNCTION ZFM_BM_RPC_EXECUTE_FORM.
*"----------------------------------------------------------------------
*"*"Local Interface:
*"  IMPORTING
*"     REFERENCE(I_LANGU) TYPE  LANGU DEFAULT 'E'
*"  CHANGING
*"     REFERENCE(C_FORMSTR) TYPE  ZST0_BM_RPC_F
*"----------------------------------------------------------------------

* Generate columns, rows
  PERFORM INIT_F_DATA CHANGING C_FORMSTR.

* Set text for leading cells
  LOOP AT C_FORMSTR-SEGITM-ROWS INTO DATA(LS_SIR)
    WHERE HROW IS INITIAL.
    LOOP AT C_FORMSTR-SEGITM-COLUMNS INTO DATA(LS_SIC)
      WHERE LEADCOL IS NOT INITIAL AND FIELDNAME CS 'COL'.
      READ TABLE C_FORMSTR-DATA ASSIGNING FIELD-SYMBOL(<LF_DATA>)
        WITH KEY ROWPOS = LS_SIR-ROWNO.
      IF SY-SUBRC IS INITIAL.
        IF I_LANGU = 'E'.
          ASSIGN COMPONENT LS_SIC-FIELDNAME OF STRUCTURE LS_SIR
            TO FIELD-SYMBOL(<LF_LEADCELL>).
        ELSE.
          ASSIGN COMPONENT LS_SIC-FIELDNAME OF STRUCTURE LS_SIR-VIDATA
            TO <LF_LEADCELL>.
        ENDIF.
        ASSIGN COMPONENT LS_SIC-FIELDNAME OF STRUCTURE <LF_DATA>
          TO FIELD-SYMBOL(<LF_VALUE>).
        <LF_VALUE> = <LF_LEADCELL>.

        PERFORM CELL_CONVERT_VARIABLE
          USING C_FORMSTR
          CHANGING <LF_VALUE>.
      ENDIF.
    ENDLOOP.
  ENDLOOP.

* Calculate except formula fields
  LOOP AT C_FORMSTR-SEGITM-ROWS INTO LS_SIR
    WHERE HROW IS INITIAL AND FIGTY <> GC_FIGTY_FORMULA.
*   Gen data for all figures
    LOOP AT C_FORMSTR-SEGITM-COLUMNS INTO LS_SIC
      WHERE LEADCOL IS INITIAL AND FIGTY <> GC_FIGTY_FORMULA.
      READ TABLE C_FORMSTR-DATA ASSIGNING <LF_DATA>
        WITH KEY ROWPOS = LS_SIR-ROWNO.
      IF SY-SUBRC IS INITIAL.
        ASSIGN COMPONENT LS_SIC-FIELDNAME OF STRUCTURE <LF_DATA>
          TO <LF_VALUE>.
        CALL FUNCTION 'ZFM_BM_RPC_EXECUTE_CELL'
          EXPORTING
            I_SIR      = LS_SIR
            I_SIC      = LS_SIC
            I_RPC_F    = C_FORMSTR
          IMPORTING
            E_CELLDATA = <LF_VALUE>.
      ENDIF.
    ENDLOOP.
  ENDLOOP.

* Calculate for cell with 1 formula dimension (row or column)
  LOOP AT C_FORMSTR-SEGITM-ROWS INTO LS_SIR
    WHERE HROW IS INITIAL.
    LOOP AT C_FORMSTR-SEGITM-COLUMNS INTO LS_SIC
      WHERE LEADCOL IS INITIAL.
      CHECK ( LS_SIR-FIGTY    = GC_FIGTY_FORMULA
           OR LS_SIC-FIGTY    = GC_FIGTY_FORMULA )
         AND LS_SIR-FIGTY <> LS_SIC-FIGTY.
      READ TABLE C_FORMSTR-DATA ASSIGNING <LF_DATA>
        WITH KEY ROWPOS = LS_SIR-ROWNO.
      IF SY-SUBRC IS INITIAL.
        ASSIGN COMPONENT LS_SIC-FIELDNAME OF STRUCTURE <LF_DATA>
          TO <LF_VALUE>.
        CALL FUNCTION 'ZFM_BM_RPC_EXECUTE_CELL'
          EXPORTING
            I_SIR      = LS_SIR
            I_SIC      = LS_SIC
            I_RPC_F    = C_FORMSTR
          IMPORTING
            E_CELLDATA = <LF_VALUE>.
      ENDIF.
    ENDLOOP.
  ENDLOOP.

* Calculate for cell with 2 formula dimension (row and column)
  LOOP AT C_FORMSTR-SEGITM-ROWS INTO LS_SIR
    WHERE HROW IS INITIAL AND FIGTY    = GC_FIGTY_FORMULA.
    LOOP AT C_FORMSTR-SEGITM-COLUMNS INTO LS_SIC
      WHERE LEADCOL IS INITIAL AND FIGTY    = GC_FIGTY_FORMULA.
      READ TABLE C_FORMSTR-DATA ASSIGNING <LF_DATA>
        WITH KEY ROWPOS = LS_SIR-ROWNO.
      IF SY-SUBRC IS INITIAL.
        ASSIGN COMPONENT LS_SIC-FIELDNAME OF STRUCTURE <LF_DATA>
          TO <LF_VALUE>.
        CALL FUNCTION 'ZFM_BM_RPC_EXECUTE_CELL'
          EXPORTING
            I_SIR      = LS_SIR
            I_SIC      = LS_SIC
            I_RPC_F    = C_FORMSTR
          IMPORTING
            E_CELLDATA = <LF_VALUE>.
      ENDIF.
    ENDLOOP.
  ENDLOOP.

ENDFUNCTION.
